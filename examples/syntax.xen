// This file showcases the entire syntax of the Xen programming language
// All lines must end in a semicolon

// Packages are imported like so:
include io;
include math;
include string;
include datetime;
include array;

// Global variables are defined like so:
var ITERS = 1000;

// Loops
for (var i = 0; i < ITERS; i++) {
    io.println(i); // print text to console
}

// range based loop
for (var i in 0..ITERS) {
    io.println(i);
}

// arrays
var items = ["apple", "banana", "orange"];

// range based array iteration
for (var i in items) {
    io.println(i);
}

// while loop
while (array.len(items) > 0) {
   var item = array.pop(items);
   io.println(item);
}

// Functions
fn say_hello() {
    io.print("Enter your name: ");
    var name = io.readline();
    io.println("Hello, ", name, "!");
}

//say_hello();

// You can cast primitives and strings to numbers with `number()`
io.print("Enter a number: ");
var num = number(io.readline());
io.println(typeof(num), " ", num);

// Functions as return values
fn add_nums(a, b) {
    return a + b;
}

fn add_strings(a, b) {
    return string.join(a, b);
}

fn get_add_func(type) {
    if (type == "number") {
        return add_nums;
    } else if (type == "string") {
        return add_strings;
    } else {
        return null;
    }
}

var a = 10;
var b = 20;
var add_fn = get_add_func(typeof(a));
if (add_fn != null) {
    io.println(add_fn(a, b));
}

var str1 = "hello ";
var str2 = "world";
add_fn = get_add_func(typeof(str1));
io.println(add_fn(str1, str2));

// Nested functions
fn outer() {
    io.println("outer");

    fn inner() {
        io.println("inner");
    }

    inner();
}

outer();

// Compound operators and inc/dec
var a = 5;
io.println("var a = ", a);
var b = a++;  // b = 5, a = 6
io.println("var b = a++: ", b);
var c = ++a;  // c = 7, a = 7
io.println("var c = ++a: ", c);

var x = 10;
io.println("var x = ", x);
x += 5;
io.println("x += 5: ", x);
x -= 2;
io.println("x -= 2: ", x);
x *= 3;
io.println("x *= 3: ", x);
x /= 2;
io.println("x /= 2: ", x);
x %= 7;
io.println("x %= 7: ", x);
